from snakemake.utils import min_version

##### set minimum snakemake version #####
min_version("8.4.1")


configfile: "config/config.yaml"


include: "rules/common.smk"


rule all:
    input:
        get_final_output(),


rule harmonize_sumstats:
    input:
        sumstats=get_sumstats,
    output:
        "results/pickle/{sample}.pkl",
    conda:
        "scripts/gwaspipe/environment.yml"
    params:
        format=config.get("params").get("harmonize_sumstats").get("input_format"),
        config_file=config.get("params").get("harmonize_sumstats").get("config_file"),
    shell:
        "python workflow/scripts/gwaspipe/src/gwaspipe.py "
        "-f {params.format} "
        "-c {params.config_file} "
        "-i {input.sumstats}"


rule write_if_output_and_plots:
    input:
        "results/pickle/{sample}.pkl",
    output:
        temp("results/if/report_if_{sample}.txt"),
        "results/plots/{sample}.png",
    conda:
        "scripts/gwaspipe/environment.yml"
    params:
        format=config.get("params").get("write_if_output_and_plots").get("input_format"),
        config_file=config.get("params")
        .get("write_if_output_and_plots")
        .get("config_file"),
    shell:
        "python workflow/scripts/gwaspipe/src/gwaspipe.py "
        "-f {params.format} "
        "-c {params.config_file} "
        "-i {input}"


rule create_if_table:
    input:
        expand("results/if/report_if_{sample}.txt", sample=samples.sample_name),
    output:
        "results/if/inflation_factors_table.tsv",
    conda:
        "envs/create_inflation_factors_table.yaml"
    shell:
        "python workflow/scripts/create_inflation_factors_table.py -i results/if -o results/if/inflation_factors_table.tsv"


rule write_sumstats_output:
    input:
        "results/pickle/{sample}.pkl",
    output:
        "results/ldsc/{sample}.ldsc.tsv.gz",
        "results/metal/{sample}.metal.tsv.gz",
        "results/regenie/{sample}.regenie.tsv.gz",
    conda:
        "scripts/gwaspipe/environment.yml"
    params:
        format=config.get("params").get("write_sumstats_output").get("input_format"),
        config_file=config.get("params").get("write_sumstats_output").get("config_file"),
    shell:
        "python workflow/scripts/gwaspipe/src/gwaspipe.py "
        "-f {params.format} "
        "-c {params.config_file} "
        "-i {input}"


rule bgzip_tabix:
    input:
        "results/regenie/{sample}.regenie.tsv.gz",
    output:
        "results/regenie/{sample}.regenie.tsv.gz.tbi",
    conda:
        "envs/bgzip_tabix.yaml"
    threads: 8
    shell:
        "workflow/scripts/bgzip_tabix.sh {input} {threads}"
