from snakemake.utils import min_version

##### set minimum snakemake version #####
min_version("8.4.1")


configfile: "config/config.yaml"


include: "rules/common.smk"


rule all:
    input:
        get_final_output(),


rule harmonize_sumstats:
    input:
        sumstats=get_sumstats,
    output:
        "results/pickle/{sample}.pkl",
        "results/outputs/{sample}.regenie.tsv.gz",
        "results/if/report_if_{sample}.txt",
        "results/plots/{sample}.png",
    conda:
        "scripts/gwaspipe/environment.yml"
    params:
        format=config.get("params").get("harmonize_sumstats").get("input_format"),
        config_file=config.get("params").get("harmonize_sumstats").get("config_file"),
    shell:
        "python workflow/scripts/gwaspipe/src/gwaspipe.py "
        "-f {params.format} "
        "-c {params.config_file} "
        "-i {input.sumstats}"


rule create_if_table:
    input:
        expand("results/if/report_if_{sample}.txt", sample=samples.sample_name),
    output:
        "results/if/inflation_factors_table.tsv",
    conda:
        "envs/create_inflation_factors_table.yaml"
    shell:
        "python workflow/scripts/create_inflation_factors_table.py -i results/if -o results/if/inflation_factors_table.tsv"


rule convert_sumstats:
    input:
        "results/pickle/{sample}.pkl",
    output:
        "results/ldsc/{sample}.ldsc.tsv.gz",
        "results/metal/{sample}.metal.tsv.gz",
    conda:
        "scripts/gwaspipe/environment.yml"
    params:
        format=config.get("params").get("convert_sumstats").get("input_format"),
        config_file=config.get("params").get("convert_sumstats").get("config_file"),
    shell:
        "python workflow/scripts/gwaspipe/src/gwaspipe.py "
        "-f {params.format} "
        "-c {params.config_file} "
        "-i {input}"


rule bgzip_tabix:
    input:
        "results/outputs/{sample}.regenie.tsv.gz",
    output:
        "results/outputs/{sample}.regenie.tsv.gz.tbi",
    conda:
        "envs/bgzip_tabix.yaml"
    shell:
        "workflow/scripts/bgzip_tabix.sh {input} {threads}"
