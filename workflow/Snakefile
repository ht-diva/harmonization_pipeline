from snakemake.utils import min_version

##### set minimum snakemake version #####
min_version("8.4.1")


containerized: "docker://ghcr.io/ht-diva/pqtl_pipeline:1c42525"


configfile: "config/config.yaml"


include: "rules/common.smk"
include: "rules/metal.smk"
include: "rules/ldsc.smk"
include: "rules/tiledb.smk"
include: "rules/finemapping.smk"


# include: "rules/move.smk"


rule all:
    input:
        get_final_output(),
    resources:
        runtime=3,


rule harmonize_sumstats:
    input:
        sumstats=get_sumstats,
    output:
        ws_path("pickle/{seqid}.pkl"),
        ws_path("outputs/{seqid}/{seqid}.regenie.tsv.gz"),
        ws_path("if/report_if_{seqid}.txt"),
        ws_path("plots/{seqid}.png"),
    conda:
        "scripts/gwaspipe/environment.yml"
    params:
        format=config.get("params").get("harmonize_sumstats").get("input_format"),
        config_file=config.get("params").get("harmonize_sumstats").get("config_file"),
        output_path=config.get("workspace_path"),
    resources:
        runtime=lambda wc, attempt: attempt * 60,
    shell:
        "python workflow/scripts/gwaspipe/src/gwaspipe.py "
        "-f {params.format} "
        "-c {params.config_file} "
        "-i {input.sumstats} "
        "-o {params.output_path}"


rule create_if_table:
    input:
        expand(ws_path("if/report_if_{seqid}.txt"), seqid=analytes.seqid),
    output:
        ws_path("inflation_factors_table.tsv"),
    conda:
        "envs/create_inflation_factors_table.yaml"
    params:
        input_path=ws_path("if"),
    shell:
        "python workflow/scripts/create_inflation_factors_table.py -i {params.input_path} -o {output}"


rule save_min_pvalue:
    input:
        ws_path("pickle/{seqid}.pkl"),
    output:
        ws_path("min_P/{seqid}/{seqid}.nsmallest.tsv"),
    conda:
        "scripts/gwaspipe/environment.yml"
    params:
        format=config.get("params").get("save_min_pvalue").get("input_format"),
        config_file=config.get("params").get("save_min_pvalue").get("config_file"),
        output_path=config.get("workspace_path"),
    resources:
        runtime=lambda wc, attempt: attempt * 30,
    shell:
        "python workflow/scripts/gwaspipe/src/gwaspipe.py "
        "-f {params.format} "
        "-c {params.config_file} "
        "-i {input} "
        "-o {params.output_path}"


rule bgzip_tabix:
    input:
        ws_path("outputs/{seqid}/{seqid}.regenie.tsv.gz"),
    output:
        ws_path("outputs/{seqid}/{seqid}.regenie.tsv.gz.tbi"),
    conda:
        "envs/bgzip_tabix.yaml"
    shell:
        "workflow/scripts/bgzip_tabix.sh {input} {threads}"
